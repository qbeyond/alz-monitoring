// more or equal than 90% Diskspace used (Unix)
let VMs = MonitoringResources_CL 
    | where TimeGenerated > ago(13h)
    | where type_s in~ ("microsoft.compute/virtualmachines", "microsoft.hybridcompute/machines", "microsoft.compute/virtualmachinescalesets")
    | extend id_s = toupper(id_s) 
    | summarize arg_max(TimeGenerated, *) by id_s
    | project TimeGenerated, id_s, name_s, tags_managedby_s, tags_alerting_s;
let mountpoints = dynamic(["/", "/var", "/boot", "/tmp", "/home", "/opt", "/backup", "/boot/efi"]);
let warning = int(80);
let critical = int(90);
InsightsMetrics
| extend Mountpoint = tostring(parse_json(Tags)["vm.azm.ms/mountId"])
| where Namespace == "LogicalDisk"
| where Name == "FreeSpacePercentage"
| where Mountpoint in (mountpoints)
| extend InverseVal = (100 - Val)
| where InverseVal >= (warning)
| where TimeGenerated > ago(5m)
| extend affected_object = toupper(tostring(split(_ResourceId, "/")[-1]))
| summarize arg_max(TimeGenerated, *) by affected_object, Mountpoint
| extend _ResourceId = toupper(_ResourceId)
| project _ResourceId, affected_object, InverseVal, Mountpoint
| join kind= leftouter VMs on $left._ResourceId == $right.id_s
| where tags_managedby_s =~ "q.beyond" or isempty(tags_managedby_s) 
| where tags_alerting_s =~ "enabled" or isempty(tags_alerting_s)
| extend state = case(InverseVal >= (critical), "Critical", InverseVal <= (critical), "Warning", "")
| extend threshold = case(InverseVal >= (critical), critical, InverseVal < (critical), warning, warning)
| extend monitor_package = "AZ_UX_SC_ManagedOSUnix"
| extend monitor_name = "AZ_UNIX_FILESPACE"
| extend monitor_description = strcat("Disk threshold has been reached. More or equal than ", (threshold), "% Diskspace used. Check the Diskspace of the Mountpoint, please.")
| extend script_name = "n/a"
| extend script_version = "n/a"
| extend value = round(InverseVal, 2)
| extend additional_information = strcat("more or equal than ", (threshold), "% Diskspace used. Managed by ", (tags_managedby_s), ".")
| extend affected_entity = Mountpoint
| project _ResourceId, RawData = strcat("TimeGenerated:", now(), ";_ResourceId:", _ResourceId, ";state:", state, ";affected_object:", affected_object, ";monitor_package:", monitor_package, ";monitor_name:", monitor_name, ";monitor_description:", monitor_description, ";script_name:", script_name, ";script_version:", script_version, ";threshold:", threshold, ";value:", value, ";affected_entity:", affected_entity, ";additional_information:", additional_information)

