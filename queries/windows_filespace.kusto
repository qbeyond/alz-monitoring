// less or equal than 10% Diskspace free (Windows)
let VMs = MonitoringResources_CL 
| where TimeGenerated > ago(13h)
| where type_s in~ ("microsoft.compute/virtualmachines", "microsoft.hybridcompute/machines", "microsoft.compute/virtualmachinescalesets")
| extend id_s = toupper(id_s) 
| summarize arg_max(TimeGenerated,*) by id_s
| project TimeGenerated, id_s, name_s, tags_managedby_s, tags_alerting_s;
let Disks = dynamic(["C:"]);
let warning = int(10);
let critical = int(5);
InsightsMetrics
| where TimeGenerated > ago(5m)
| where Namespace == "LogicalDisk"
| where Name == "FreeSpacePercentage"
| extend Mountpoint = tostring(parse_json(Tags)["vm.azm.ms/mountId"])
| where Mountpoint in (Disks)
| where Val <= (warning)
| extend state = case(Val > (critical), "Warning", Val <= (critical), "Critical", "Warning")
| extend threshold = case(Val > (critical), warning, Val <= (critical), critical, warning)
| extend affected_object = toupper(tostring(split(_ResourceId, "/") [-1]))
| extend _ResourceId = toupper(_ResourceId)
| summarize arg_max(TimeGenerated, *) by affected_object, Mountpoint
| project _ResourceId, affected_object, Val, threshold, Mountpoint, state
| join kind= leftouter VMs on $left._ResourceId == $right.id_s
| where tags_alerting_s =~ "enabled" or isempty(tags_alerting_s)
| where tags_managedby_s =~ "q.beyond" or isempty(tags_managedby_s) 
| extend monitor_package = "AZ_NT_BASEPOLICY"
| extend monitor_name = "AZ_NT_DISKS"
| extend monitor_description = strcat("Disk threshold has been reached. Less or equal than ", (threshold), "% Diskspace free. Please, check the Diskspace.")
| extend script_name = "n/a"
| extend script_version = "n/a"
| extend value = round(Val, 2)
| extend additional_information = strcat("Only ", (value), " % Diskspace free. Managed by ",(tags_managedby_s),".")
| extend affected_entity = Mountpoint
| project TimeGenerated = now(), _ResourceId, state, affected_object, monitor_package, monitor_name, monitor_description, script_name, script_version, threshold, value, affected_entity, additional_information